/**
* Класс, который декодиует изображение jpeg
* Так как вычисления происходят с дробными числами, а так же матрицами (в упрощенном виде), работает очень долго
*/

class Decoder {
    field int width, height, dccoeff;
    field Array tables, quantizationTable;

    constructor Decoder new() {
        let tables = Array.new(4);
        let quantizationTable = Array.new(64);
        return this;
    }

    method void Decode () {
        // Тут надо считывать байты из изображения с помощью вспомогательного класса Bytes
        var int byteIndex, header1, header2, chunkLen, chunkIndex;
        let byteIndex = 0;
        while(true){
            let header1 = Bytes.GetByte(byteIndex);
            let header2 = Bytes.GetByte(byteIndex+1);
            if (header1 = 255 & header2 = 216) { // start of file: marker = FFD8
                let chunkLen = 2;
            }
            else{
                if (header1 = 255 & header2 = 217) { // end of file: marker = FFD9
                    return;
                } else {
                    let chunkLen = 256 * Bytes.GetByte(byteIndex + 2) + Bytes.GetByte(byteIndex + 3) + 2;
                    let chunkIndex = byteIndex + 4;

                    if (header1 = 255){
                        if (header2 = 219) { do DefineQuantizationTables(chunkIndex, chunkLen - 4);} // quantization table: marker = FFDB
                        if (header2 = 192) { do BaseLineDCT(chunkIndex, chunkLen - 4); } // marker = FFC0
                        if (header2 = 196) { do DefineHuffmanTables(chunkIndex, chunkLen - 4); } // marker = FFC4
                        if (header2 = 218) { let chunkLen = StartOfScan(byteIndex, chunkLen); }
                        } 
                    }
                }
            let byteIndex = byteIndex + chunkLen;
            
            if (Bytes.GetByte(byteIndex) = -1) { return; }
            }
        return;
    }

    method IDCT BuildMatrix(Stream stream, int oldccoeff, int id) {
        var IDCT inverseDCT;
        var HuffmanTable DC, AC;
        var int code, bits, l, coeff;

        let inverseDCT = IDCT.new();
        let DC = tables[id];
        let AC = tables[id + 1];
        let code = DC.GetCode(stream);
        //do Output.printInt(code);
        let bits = stream.GetBinN(code);

        let dccoeff = Extensions.DecodeNumber(code, bits) + oldccoeff;

        do inverseDCT.AddZigZag(0, dccoeff * quantizationTable[0]);
        let l = 1;
        while(l < 64) {
            let code = AC.GetCode(stream);
            if (code = 0) {
                return inverseDCT;
            }

            if (code > 15) {
                let l = l + (code / 16);
                let code = code & 15;
            }

            let bits = stream.GetBinN(code);

            if (l < 64) {
                let coeff = Extensions.DecodeNumber(code, bits);
                do inverseDCT.AddZigZag(l, coeff * quantizationTable[l]);
                let l = l + 1;
            }
        }
        return inverseDCT;
    }

    method int StartOfScan (int chunkIndex, int chunkLen) {
        var Stream stream;
        var IDCT matL, mat1;
        var int oldLumdcCoeff, y, x, yy, xx, blocksHeight, blocksWidth, trash1, trash2;
        var Float color;

        let stream = Stream.new(chunkIndex + chunkLen);
        
        let oldLumdcCoeff = 0;
        let trash1 = 0;
        let trash2 = 0;

        let blocksHeight = height / 8;
        let blocksWidth = width / 8;
        let y = 0;
        while(y < blocksHeight) {
            
            let x = 0;
            while(x < blocksWidth) {
                let matL = BuildMatrix(stream, oldLumdcCoeff, 0);
                let oldLumdcCoeff = dccoeff;
                let mat1 = BuildMatrix(stream, trash1, 1);
                let trash1 = dccoeff;
                do mat1.dispose();
                let mat1 = BuildMatrix(stream, trash2, 1);
                let trash2 = dccoeff;
                do mat1.dispose();
                
                let yy = 0;
                while(yy < 8) {
                    let xx = 0;
                    while(xx < 8) {
                        let color = matL.GetFromBase(Extensions.XYtoLin(xx, yy));
                        if (color.isNeg()) { 
                            do Screen.setColor(false); 
                        }
                        else { 
                            do Screen.setColor(true); 
                        }
                        do color.dispose();

                        do Screen.drawPixel(x * 8 + xx, y * 8 + yy);
                        let xx = xx + 1;
                    }

                    let yy = yy + 1;
                }
                do matL.dispose();
                let x = x + 1;
            }
            let y = y + 1;
        }
        return Bytes.GetLen() - chunkIndex - 2; 
    }

    method void DefineQuantizationTables (int chunkIndex, int chunkLen) {
        var int chunkPointer, header;
        let chunkPointer = chunkIndex;
        while(chunkPointer < (chunkIndex + chunkLen)) {
            let header = Bytes.GetByte(chunkPointer);
            if (header = 0) {
                let quantizationTable = Extensions.GetArray(chunkPointer + 1, chunkPointer + 1 + 64, 64);
            }
            let chunkPointer = chunkPointer + 65;
        }
        
        return;
    }

    method void BaseLineDCT(int chunkIndex, int chunkLen) {
        var int header, components, i, id, samp, QtbId;
        
        let header = Bytes.GetByte(chunkIndex);
        let height = Bytes.GetByte(chunkIndex + 1) * 256 + Bytes.GetByte(chunkIndex + 2);
        let width = Bytes.GetByte(chunkIndex + 3) * 256 + Bytes.GetByte(chunkIndex + 4);
        return;
    }

    method void DefineHuffmanTables(int dataIndex, int dataLength) {
        var int off, header, sumLengths, i, j, staticOff, elementsIndex; 
        var Array lengths, elements, tempArray;
        var HuffmanTable hf;
        let lengths = Array.new(16);

        let off = dataIndex;

        while (off < (dataIndex + dataLength)) {
            let header = Bytes.GetByte(off);
            let off = off + 1;

            let lengths = Extensions.GetArray(off,off+16,16);
            let off = off + 16;

            let sumLengths = 0;
            let i = 0;
            while (i < 16) {
                let sumLengths = sumLengths + lengths[i];
                let i = i + 1;
            }
            let elements = Array.new(sumLengths); 
            let i = 0;
            let elementsIndex = 0;
            while(i < 16) {
                
                if(lengths[i] > 0) {
                    let tempArray = Extensions.GetArray(off, off+lengths[i], lengths[i]);                

                    let j = 0; //добавляем значения tempArray в конец elements
                    while (j < lengths[i]){
                        let elements[elementsIndex] = tempArray[j];
                        let j = j + 1;
                        let elementsIndex = elementsIndex + 1;
                    }
                    do tempArray.dispose();
                }
                let off = off + lengths[i];

                let i = i + 1;
            }            
            let hf = HuffmanTable.new();
            do hf.GetHuffmanBits(lengths, elements);
            if (header = 0) {
                let tables[0] = hf; // DC lumin table
            }
            if (header = 16) {
                let tables[1] = hf; // AC lumin table
            }
            if (header = 1) {
                let tables[2] = hf; // DC chroma table
            }
            if (header = 17) {
                let tables[3] = hf; // AC chroma table
            }
        }
        return;
    }
}